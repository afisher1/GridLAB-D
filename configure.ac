# $Id: configure.ac,v 1.31 2008/01/25 18:05:07 d3p181 Exp $                                            -*- Autoconf -*-
# Copyright (C) 2008 Battelle Memorial Institute
# Process this file with autoconf to produce a configure script.
# This file is distributed under the same terms as GridLAB-D.

# Parse version info from core/version.h into ./version.sh
esyscmd([sh -c 'cat core/version.h | tr -d "\r" | while read DEFINE NAME VALUE EXTRA; do [ "$DEFINE" != "#define" ] || case $NAME in REV_*|BRANCH) echo ${NAME#REV_}=$VALUE;; esac; done > version.sh; echo VERSION=\"\$MAJOR.\$MINOR.\$PATCH\" >> version.sh'])
# Define M4 macros for version info
define([pkgversion], esyscmd([sh -c ". ./version.sh; echo \$VERSION | tr -d '\n'"]))dnl
define([pkgbranch], esyscmd([sh -c ". ./version.sh; echo \$BRANCH | tr -d '\n'"]))dnl
define([svnversion], esyscmd([sh -c "svnversion | sed 's/^exported$/0/;s/[^A-Za-z0-9]/_/g' | tr -d '\n'"]))dnl
# Export version info as autoconf variables
AC_DEFINE(PACKAGE_BASEVERSION, [pkgversion], [Package Base Version])
AC_DEFINE(PACKAGE_BRANCH, [pkgbranch], [Package Branch Name])
AC_DEFINE(PACKAGE_REVISION, [svnversion], [SVN Revision])

AC_PREREQ(2.59)
AC_INIT([Gridlab-D], [pkgversion.svnversion], [David Chassin <david.chassin@pnl.gov>], gridlabd)
AC_CONFIG_SRCDIR([core/aggregate.c])
# Older versions of automake may require the that following macro be replaced
# with AM_CONFIG_HEADER([core/config.h]).
AC_CONFIG_HEADER([core/config.h])
AC_CONFIG_MACRO_DIR([m4])
AM_INIT_AUTOMAKE([foreign])
AC_DISABLE_STATIC

AC_PREFIX_DEFAULT([/usr])
AC_CANONICAL_HOST

AC_ARG_ENABLE([debug],
				  AS_HELP_STRING([--enable-debug],
				  					  [turn on debugging @<:@default=yes@:>@]),
				  [case "${enableval}" in
				  	 yes) debug=true ;;
				  	 no) debug=false ;;
				  	 *) AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
				  	esac], [debug=true])

AC_ARG_ENABLE([optimization],
				  AS_HELP_STRING([--enable-optimization@<:@=LEVEL@:>@],
				  					  [turn on optimization @<:@default=0@:>@]),
				  [case "${enableval}" in
				  	 0|1|2|3|s) optimization=${enableval} ;;
				  	 yes) optimization=2 ;;
				  	 no) optimization=0 ;;
				  	 *) AC_MSG_ERROR([bad value ${enableval} for --enable-optimization]) ;;
				  	esac], [optimization=0])

#AC_ARG_ENABLE([cppunit],
#				  AS_HELP_STRING([--enable-cppunit],
#				  					  [turn on unit testing @<:@default=yes@:>@]),
#				  [case "${enableval}" in
#				  	 yes) WANT_CPPUNIT=true ;;
#				  	 no) WANT_CPPUNIT=false ;;
#				  	 *) AC_MSG_ERROR([bad value ${enableval} for --enable-cppunit]) ;;
#				  	esac], [WANT_CPPUNIT=true])
#AM_CONDITIONAL(WANT_CPPUNIT, test x$WANT_CPPUNIT = xtrue)

GLFLAGS="$GLFLAGS -I`pwd`/core"
if [[ "$debug" = "true" ]]; then
	GLFLAGS="$GLFLAGS -g"
fi
if [[ "$optimization" ]]; then
	GLFLAGS="$GLFLAGS -O$optimization"
fi

GLRT_LDFLAGS="-export-all-symbols"

case "$host_os" in
darwin*)
     export LIBTOOL=`which glibtool`
     export LIBTOOLIZE=`which glibtoolize`
     GLRT_LDFLAGS="-bundle"
     LIBEXT=.so
	 march=""
     ;;
cygwin*)
     AC_LIBTOOL_WIN32_DLL
     LIBEXT=.a
	 march=""
     ;;
mingw*)
     AC_LIBTOOL_WIN32_DLL
     LIBEXT=.dll
	 march=""
     ;;
*)
     LIBEXT=.so
	 case "$host_cpu" in
		i386) AC_MSG_FAILURE([Sorry, i386 is not a supported architecture]);;
		i486|i586|i686) march="-march=$host_cpu";;
		*) march="";;
	 esac
     ;;
esac

# Putting -ansi in CPPFLAGS causes errors that prevent building.
#CPPFLAGS="$CPPFLAGS $GLFLAGS -ansi"
CPPFLAGS="$CPPFLAGS $GLFLAGS $march"
CXXFLAGS="$CXXFLAGS $GLFLAGS $march"
CFLAGS="$CFLAGS $GLFLAGS $march"

AC_DEFINE_UNQUOTED([REALTIME_LDFLAGS], ["$GLRT_LDFLAGS"], [Realtime linker flags])
AC_SUBST(LIBEXT)
AC_DEFINE_UNQUOTED([DLEXT], ["$LIBEXT"], [Dynamic library extension])

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_LN_S
#AC_PROG_MKDIR_P
AC_PROG_INSTALL
AC_PROG_LIBTOOL

if `$LD --version | grep -q '^GNU ld'`; then
	LDFLAGS="$LDFLAGS -Wl,--no-undefined"
	export LDFLAGS
fi

AX_LIB_XERCES
test "$HAVE_XERCES" = "yes" || \
		AC_MSG_FAILURE([Xerces library not found. If Xerces is not properly \
installed, you probably need to use one or more of the --with-xerces options. \
Try `./configure --help[`] for more information.])

DX_INIT_DOXYGEN(gridlabd, [doxygen/gridlabd.conf])
DX_HTML_FEATURE([ON])
DX_CHM_FEATURE([OFF])
DX_CHI_FEATURE([OFF])
DX_MAN_FEATURE([OFF])
DX_RTF_FEATURE([OFF])
DX_XML_FEATURE([OFF])
DX_PDF_FEATURE([OFF])
DX_PS_FEATURE([OFF])

# Checks for libraries.
AC_LANG_PUSH([C++])
AC_CHECK_HEADER([cppunit/config-auto.h], [],
   [CPPFLAGS="$CPPFLAGS -D_NO_CPPUNIT"])
#   [AC_MSG_FAILURE([Missing cppunit headers.])])
#AC_CHECK_LIB([cppunit], [CppUnit::TestFactoryRegistry::getRegistry])
LIBS_ORIG=$LIBS
echo "LIBS: " $LIBS
LIBS="-ldl -lcppunit $LIBS"
echo "LIBS: " $LIBS
AC_LINK_IFELSE(AC_LANG_PROGRAM([[#include <cppunit/extensions/TestFactoryRegistry.h>]],[[CppUnit::Test *suite = CppUnit::TestFactoryRegistry::getRegistry().makeTest();]]),[],[LIBS=$LIBS_ORIG])
#LIBS=$LIBS_ORIG
AC_LANG_POP([C++])

#AM_PATH_CPPUNIT(1.12.0)
#AC_CHECK_LIB([cppunit], [main], [],
#   [AC_MSG_FAILURE([Missing cppunit shared library.])])

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([malloc.h memory.h stdlib.h string.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_STRUCT_TM

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_MKTIME
AC_FUNC_REALLOC
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([isfinite atexit getcwd memset pow putenv sqrt strchr strerror strrchr tzset])
ACX_PTHREAD
GLD_GET_NPROCS
#GCC_CT_FEATURES

# DO NOT EDIT THE FOLLOWING LINE
AC_CONFIG_FILES([./Makefile \
	core/Makefile \
	core/rt/Makefile \
	assert/Makefile \
	climate/Makefile \
	climate/tmy/Makefile \
	commercial/Makefile \
	network/Makefile \
	powerflow/Makefile \
	residential/Makefile \
	reliability/Makefile \
	generators/Makefile\
	plc/Makefile \
	market/Makefile \
	tape_file/Makefile \
	tape_plot/Makefile \
	tape/Makefile \
	third_party/CBLAS/Makefile \
	third_party/superLU_MT/Makefile \
	gridlabd.spec \
	])

AC_SUBST(CPPFLAGS)
AC_OUTPUT

